buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'

sourceSets {
    main { compileClasspath += configurations.provided }
}

def isWercker = "true".equals(System.getenv("WERCKER"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    dexOptions {
        // Skip pre-dexing when running on Wercker CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isWercker
    }

    defaultConfig {
        targetSdkVersion 23
        minSdkVersion 9
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"
}

task install(type: Exec, dependsOn: assemble) {
    executable = 'mvn'
    args = [
            "install:install-file",
            "-DgroupId=com.uphyca.galette",
            "-DartifactId=galette",
            "-Dversion=${VERSION_NAME}",
            "-Dpackaging=aar",
            "-Dfile=build/outputs/aar/galette-release.aar"
    ]
}

apply from: 'https://raw.githubusercontent.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
// taken from https://github.com/chrisbanes/gradle-mvn-push/issues/39
afterEvaluate { project ->
    androidJavadocs.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

