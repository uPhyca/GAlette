buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
    }
}

apply plugin: 'com.android.library'

sourceSets {
    main { compileClasspath += configurations.provided }
}

def isWercker = "true".equals(System.getenv("WERCKER"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    dexOptions {
        // Skip pre-dexing when running on Wercker CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isWercker
    }

    defaultConfig {
        targetSdkVersion 21
        minSdkVersion 9
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    provided "com.android.support:support-v4:${supportVersion}"
    compile "com.google.android.gms:play-services-base:${playServicesVersion}@aar"
}

task install(type: Exec, dependsOn: assemble) {
    executable = 'mvn'
    args = [
            "install:install-file",
            "-DgroupId=com.uphyca.galette",
            "-DartifactId=galette",
            "-Dversion=${VERSION_NAME}",
            "-Dpackaging=aar",
            "-Dfile=build/outputs/aar/galette-release.aar"
    ]
}

apply from: 'https://raw.githubusercontent.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
//apply from: 'https://raw.githubusercontent.com/chrisbanes/gradle-mvn-push/44a1e74d01afd31ae1b78e6d85d1ab992295bb82/gradle-mvn-push.gradle'
